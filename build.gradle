buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'net.dongliu'
version = '4.5.0'

sourceCompatibility = 1.8

repositories {

    maven { url "http://svn.corp.qunar.com:8081/nexus/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.1'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.1'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    testCompile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.0.M0'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.0.M0'
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.1'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.1'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.1'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'requests'
                packaging 'jar'
                description 'Easy http client for java'
                url 'https://github.com/caoqianli/requests'

                scm {
                    connection 'scm:git:https://github.com/caoqianli/requests'
                    developerConnection 'scm:git:https://github.com/caoqianli/requests'
                    url 'https://github.com/caoqianli/requests'
                }

                licenses {
                    license {
                        name 'The BSD2-Clause License'
                        url 'https://github.com/caoqianli/requests/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'caoqianli'
                        name 'Liu Dong'
                        email 'dongliu@live.cn'
                    }
                }
            }
        }
    }
}

task release {
    dependsOn uploadArchives
    doLast {
        closeAndPromoteRepository.execute()
    }
}
